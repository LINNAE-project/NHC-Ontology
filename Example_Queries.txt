
1). Show me a list of all species recorded by Heinrich Kuhl (http://viaf.org/viaf/45106482), and the pages where they can be found:

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX nc: <http://makingsense.liacs.nl/rdf/nc#>
PREFIX viaf: <http://viaf.org/viaf/>
SELECT DISTINCT ?label ?page WHERE { 
       ?identification dwciri:toTaxon ?taxon .
       ?taxon rdfs:label ?label .
       ?taxon nhc:taxonRank nc:species . 
       ?organism dsw:hasIdentification ?identification .
       ?organism dsw:hasOccurrence ?occurrence .
       ?occurrence dwciri:recordedBy viaf:45106482 .
       ?occurrence dsw:hasEvidence ?observationRecord . 
       ?anno oa:hasBody ?observationRecord . 
       ?anno oa:hasTarget ?page
}
      
2). Show me a list of all recorded taxa by Heinrich Kuhl (http://viaf.org/viaf/4506482) and the pages where these can be found.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX nc: <http://testingsense.liacs.nl/rdf/nc#>
PREFIX dwc: <http://rs.tdwg.org/dwc/>
PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX viaf: <http://viaf.org/viaf/>
PREFIX oa: <http://www.w3.org/ns/oa#> 
SELECT DISTINCT ?label ?page WHERE {
       ?taxon rdfs:label ?label .
       ?identification dwciri:toTaxon ?taxon .
       ?identification dsw:isBasedOn ?humanobservation .
       ?annotation oa:hasBody ?humanobservation .
       ?annotation oa:hasTarget ?page .
       ?organism dsw:hasIdentification ?identification . 
       ?occurrence dsw:occurrenceOf ?organism . 
       ?occurrence dwciri:recordedBy viaf:45106482  
}

2. Show me a list of all species that belong to the order 'Chiropterae' (Bats), that are recorded by Heinrich Kuhl (http://viaf.org/viaf/45106482):

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX nc: <http://makingsense.liacs.nl/rdf/nc#>
PREFIX dwc: <http://rs.tdwg.org/dwc/>
PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX viaf: <http://viaf.org/viaf/>
SELECT DISTINCT ?label WHERE {
       ?taxon rdfs:label ?label .
       ?taxon nhc:taxonRank nc:species .
       ?taxon nhc:belongsToTaxon+ ?order .
       ?order rdfs:label ?Chiropterae .
       FILTER regex(?Chiropterae, "Chiropterae")  .
       ?identification dwciri:toTaxon ?taxon .
       ?identification dwciri:identifiedBy viaf:45106482 
}

3. Show me all pages with descriptions of species occurrences that were recorded between 1820 and 1821:

PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX oa: <http://www.w3.org/ns/oa#>
SELECT DISTINCT ?year ?page  WHERE {  
       ?event nhc:verbatimEventDate ?date .
       ?date dwc:year ?year .
       FILTER (?year >= 1820). 
       FILTER (?year <= 1821) .
       ?event dsw:eventOf ?occurrence .
       ?occurrence dsw:hasEvidence ?observationRecord . 
       ?anno oa:hasBody ?observationRecord . 
       ?anno oa:hasTarget ?page 
}

4. Provide me with a list of properties or attributes on which the classification of the genus 'Pteropus' was based in the field books: 

PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX uberon: <http://purl.obolibrary.org/obo/>
PREFIX ncit: <http://identifiers.org/ncit/>
PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?label2 ?ncitClass
WHERE { ?identification dwciri:toTaxon ?taxon .
  	?taxon rdfs:label ?label 
        FILTER regex(?label, "Pteropus") 
        ?identification dsw:isBasedOn ?token . 
        ?token dsw:hasDerivative ?measurement . 
	?measurement nhc:measuresOrDescribes ?propertyorattribute .
  	?propertyorattribute rdfs:label ?label2 .
  	?propertyorattribute rdf:type ?ncitClass .
	?ncitClass rdfs:subClassOf ncit:C20189 
}


5. Provide me with a list of anatomical entities on which the classification of the genus 'Pteropus' was based in the field books: 

PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX uberon: <http://purl.obolibrary.org/obo/>
PREFIX ncit: <http://identifiers.org/ncit/>
PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?label2 ?uberonClass
WHERE { ?identification dwciri:toTaxon ?taxon .
  	?taxon rdfs:label ?label 
        FILTER regex(?label, "Pteropus") 
        ?identification dsw:isBasedOn ?token . 
        ?token dsw:hasDerivative ?measurement . 
	?measurement nhc:measuresOrDescribes ?anatomicalentity .
  	?anatomicalentity rdfs:label ?label2 .
  	?anatomicalentity rdf:type ?uberonClass .
	?uberonClass rdfs:subClassOf uberon:UBERON_0001062
}

6. Provide me with a list of all later added identifications

PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?label
WHERE { ?organism nhc:additionalIdentification ?additional .
  	?additional dwciri:toTaxon ?taxon .
  	?taxon rdfs:label ?label
}
	
7. Provide me with a list of all taxonomical names that occur in the fieldbook, the naturalist who recorded them, the author who published/was going to publish the name and the page on which their occurrence is described.

PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX oa: <http://www.w3.org/ns/oa#>
SELECT DISTINCT ?naturalist ?taxon ?label ?authorOfTaxon ?page
WHERE { ?taxon rdf:type dwc:Taxon . 
  	?taxon rdfs:label ?label .
  	?taxon nhc:scientificNameAuthorship ?authorOfTaxon .
    	?identification dwciri:toTaxon ?taxon .
  	?identification dwciri:identifiedBy ?naturalist .
  	?observation dsw:isBasisForId ?identification .
  	?annotation oa:hasBody ?observation .
  	?annotation oa:hasTarget ?page
}

8. Which Chiroptera species were collected by person X on java?

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX nc: <http://makingsense.liacs.nl/rdf/nc#>
PREFIX dwc: <http://rs.tdwg.org/dwc/>
PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX viaf: <http://viaf.org/viaf/>
PREFIX oa: <http://www.w3.org/ns/oa#>
SELECT DISTINCT ?label ?page WHERE {
       ?taxon rdfs:label ?label .
       ?taxon nhc:taxonRank nc:species .
       ?taxon nhc:belongsToTaxon  ?order .
       ?order rdfs:label ?Chiropterae .
       FILTER regex(?Chiropterae, "Chiropterae") .
       ?identification dwciri:toTaxon ?taxon .
       ?organism dsw:hasIdentification ?identification . 
       ?occurrence dsw:occurrenceOf ?organism . 
       ?occurrence dwciri:recordedBy viaf:45106482 .
       ?occurrence dsw:atEvent ?event . 
       ?event dsw:locatedAt ?location . 
       ?location dwciri:inDescribedPlace ?place .
       ?place <http://www.geonames.org/ontology#parentFeature> ?parent .
       ?parent <http://www.geonames.org/ontology#alternateName> ?name 
       FILTER regex(str(?name), "Java", "i") 
       ?anno oa:hasTarget ?roi .
       ?roi oa:hasSource ?page . 
       ?anno oa:hasBody ?taxon 
}

9: Where did person X collect and what did he find there? 

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
PREFIX nc: <http://makingsense.liacs.nl/rdf/nc#>
PREFIX viaf: <http://viaf.org/viaf/>
SELECT DISTINCT ?specieslabel ?locationlabel ?page WHERE { 
       ?identification dwciri:toTaxon ?taxon .
       ?taxon rdfs:label ?specieslabel .
       ?taxon nhc:taxonRank nc:species . 
       ?organism dsw:hasIdentification ?identification .
       ?organism dsw:hasOccurrence ?occurrence .
       ?occurrence dwciri:recordedBy viaf:45106482 .
       ?occurrence dsw:atEvent ?event .
       ?event dsw:locatedAt ?location .
       ?location rdfs:label ?locationlabel . 
       ?anno oa:hasTarget ?roi .
       ?roi oa:hasSource ?page . 
       ?anno oa:hasBody ?taxon . 
} 

10: Show me all resources (lists, drawings and observations) concerning a specific species, e.g. “Pteropus”

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?page ?type WHERE {
       ?anno oa:hasTarget ?roi .
       ?roi oa:hasSource ?page . 
       ?anno oa:hasBody ?textualbody . 
       ?textualbody rdf:type oa:TextualTag .
       ?textualbody rdfs:label ?label
       FILTER regex(str(?label), "Pteropus", "i")
       ?page rdf:type ?type
}

11: Are the species named directly in the field or do they receive a number or temporary name?

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
SELECT DISTINCT ?page ?label WHERE {
       ?anno oa:hasTarget ?roi .
       ?roi oa:hasSource ?page . 
       ?anno oa:hasBody ?body . 
       ?body rdfs:label ?label .
       ?body rdf:type dwc:Taxon
}

12: What did person X illustrate and describe?
 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX dwciri: <http://rs.tdwg.org/dwc/iri/>
PREFIX dsw: <http://purl.org/dsw/>
PREFIX nhc: <http://makingsense.liacs.nl/rdf/nhc/>
SELECT DISTINCT ?page ?label ?person WHERE {
       ?anno oa:hasTarget ?roi .
       ?roi oa:hasSource ?page . 
       ?anno oa:hasBody ?body . 
       ?body rdfs:label ?label .
       ?body rdf:type dwc:Taxon .
       ?identification dwciri:toTaxon ?body .
       ?organism dsw:hasIdentification ?identification . 
       ?organism dsw:hasOccurrence ?occurrence .
       ?occurrence dwciri:recordedBy ?person .
       ?page rdf:type nhc:Page
}
